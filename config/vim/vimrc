syntax on
filetype plugin on

""" Options """
set fenc=utf-8
set hidden
set autoread
set showcmd
" Indentation
set autoindent
set tabstop=4
set softtabstop=4
set shiftwidth=4
set shiftround
set smarttab
set list
set listchars=tab:>-,trail:-,extends:>,precedes:<,nbsp:%
" Views
set title
set number
set relativenumber
set cursorline
set showmatch
set laststatus=2
set statusline=%F%m\ %<[%{&fenc!=''?&fenc:&enc}]\ [%Y]\ [ln:%l,col:%v]
set showtabline=2
" Controls
set mouse=a
set whichwrap=b,s,h,l,<,>,[,],~
" Search
set hlsearch
set incsearch
set smartcase
set ignorecase
" Split
set splitbelow
set splitright
" Backspace
set backspace=indent,eol,start
" Menu
set wildmenu
set wildmode=longest:full,full

" .viminfo
set viminfo+=n~/.local/share/vim/viminfo

""" Keymaps """
nnoremap ;      :
nnoremap j      gj
nnoremap k      gk
nnoremap <Down> gj
nnoremap <Up>   gk
nnoremap sh <C-w>h
nnoremap sj <C-w>j
nnoremap sk <C-w>k
nnoremap sl <C-w>l
nnoremap sH <C-w>H
nnoremap sJ <C-w>J
nnoremap sK <C-w>K
nnoremap sL <C-w>L
nnoremap <silent><C-a> ^
nnoremap <silent><C-e> $
nnoremap <silent><C-\> :vsplit<CR>
nnoremap <silent><C-_> :split<CR>
nnoremap <silent><C-h> :bprev<CR>
nnoremap <silent><C-l> :bnext<CR>
nnoremap <silent><C-w> :bdelete<CR>
inoremap jj     <ESC>
inoremap <Down> <C-\><C-o>gj
inoremap <Up>   <C-\><C-o>gk
inoremap <C-a>  <C-o>I
inoremap <C-e>  <End>

""" terminal """
command! -nargs=* T botright term <args>

""" Packages """
let g:Packages = {}
let g:PackageDir = $HOME . '/.vim/pack/plugins'

function! Package(repo, ...)
    let arg = get(a:, 1, {})
    let base = has_key(arg, 'base') ? arg.base : 'start'

    let url = 'https://github.com/' . a:repo
    let path = g:PackageDir . '/' . base . '/' . substitute(a:repo, '/', '--', 'g')

    let g:Packages[a:repo] = {
    \   'path': path,
    \   'url': url,
    \   'arg': arg,
    \ }

    if isdirectory(path)
        return
    endif

    echo 'install ' . a:repo
    echo system('git clone ' . shellescape(url) . ' ' . shellescape(path))

    if has_key(arg, 'build')
        echo system('cd ' . shellescape(path) . ';' . arg.build)
    endif
endfunction

function! PackageUpdate()
    for repo in keys(g:Packages)
        let path = g:Packages[repo].path

        echo 'update ' . repo
        echo system('git -C ' . shellescape(path) . ' pull')
    endfor
endfunction

" Editorconfig
call Package('editorconfig/editorconfig-vim')

" lightline.vim
call Package('itchyny/lightline.vim')
call Package('mengelbrecht/lightline-bufferline')

let g:lightline = {
\   'colorscheme': 'one',
\   'active': {
\       'left': [ [ 'mode', 'paste'  ], [ 'readonly', 'filename', 'modified'  ]  ]
\   },
\   'tabline': {
\       'left': [ ['buffers']  ],
\       'right': [ ['close']  ]
\   },
\   'component_expand': {
\       'buffers': 'lightline#bufferline#buffers'
\   },
\   'component_type': {
\       'buffers': 'tabsel'
\   }
\ }

" Colorschemes
call Package('tomasiser/vim-code-dark')

colorscheme codedark

" vim-gitgutter
call Package('airblade/vim-gitgutter')

" vim-fugitive
call Package('tpope/vim-fugitive')

" nerdtree
call Package('preservim/nerdtree')

nnoremap <silent><C-o> :NERDTreeToggle<CR>

" lexima.vim
call Package('cohama/lexima.vim')

" vim-expand-region
call Package('terryma/vim-expand-region')

vmap v <Plug>(expand_region_expand)
vmap V <Plug>(expand_region_shrink)

" nerdcommenter
call Package('preservim/nerdcommenter')

let g:NERDSpaceDelims = 1
let g:NERDCompactSexyComs = 1
let g:NERDDefaultAlign = 'left'

nmap <C-c> <Plug>NERDCommenterToggle
vmap <C-c> <Plug>NERDCommenterToggle<CR>gv

" fzf.vim
call Package('junegunn/fzf', { 'build': 'bash install --bin' })
call Package('junegunn/fzf.vim')

nnoremap <silent>//     :BLines<CR>
nnoremap <silent><C-f>  :Lines<CR>
nnoremap <silent><C-p>  :Files<CR>

" vim-highlightedyank
call Package('machakann/vim-highlightedyank')
